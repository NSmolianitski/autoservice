buildscript {
    dependencies {
        classpath 'org.flywaydb:flyway-database-postgresql:10.4.1'
        classpath 'org.postgresql:postgresql:42.6.0'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.flywaydb.flyway' version '10.10.0'
    id 'org.jooq.jooq-codegen-gradle' version "3.19.6"
}

group = 'co.baboon'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.jooq:jooq:3.19.6'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    runtimeOnly 'org.postgresql:postgresql'
}

flyway {
    url = 'jdbc:postgresql://localhost:5432/AutoserviceDB'
    user = 'admin'
    password = 'password'
}

jooq {
    configuration {
        jdbc {
            url = 'jdbc:postgresql://localhost:5432/AutoserviceDB'
            user = 'admin'
            password = 'password'
        }
        generator {
            database {
                inputSchema = 'public'
            }
            target {
                packageName = 'co.baboon.autoservice.jooq'
                directory = layout.buildDirectory.dir('/generated/sources/jooq').get()
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs += layout.buildDirectory.dir('/generated/sources/jooq').get()
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
